plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

def APP_NAME = "app_name"
def API_HOST = "API_HOST"
def DB_NAME = "DB_NAME"

android {
    compileSdk 33
    flavorDimensions "default"

    dataBinding{
        enabled = true
    }

    defaultConfig {
        applicationId "app.fintonic.demo"
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            lintOptions {
                checkReleaseBuilds false
            }
            setKeystoreSigningProperties(
                    './signing.properties',
                    android.signingConfigs.release)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        staging {
            debuggable true
            applicationIdSuffix = ".staging"
        }
        debug {
            debuggable true
            applicationIdSuffix  = ".quality"
        }
    }

    productFlavors.whenObjectAdded { flavor ->
        def flavorData = rootProject.ext[flavor.name]
        flavor.versionCode flavorData.versionCode
        flavor.versionName flavorData.versionName
    }

    productFlavors {
        beertonic {
            dimension "default"
        }
    }

    applicationVariants.all { variant ->
        variant.productFlavors.each { flavor ->

            def flavorData = rootProject.ext[flavor.name]
            def appName = flavorData.appName

            println("**************************************************")
            println("-------------" + flavor.name + "." + variant.buildType.name + "." + variant.mergedFlavor.versionName + "-------------")

            if (variant.buildType.name == "debug") {
                appName = "[Q] " + flavorData.appName
            } else if (variant.buildType.name == "staging") {
                appName = "[S] " + flavorData.appName
            }

            setVariantResValue(variant, appName, APP_NAME, STRING.toLowerCase())

            setVariantBuildConfigField(variant, appName, APP_NAME.toUpperCase(), STRING)
            setVariantBuildConfigField(variant, flavorData.apiHost, API_HOST, STRING)
            setVariantBuildConfigField(variant, flavorData.dbName, DB_NAME, STRING)
            println("**************************************************")
        }

        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}-${variant.versionCode}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.7.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.3'
    implementation 'androidx.activity:activity-ktx:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'org.koin:koin-androidx-scope:2.2.2'
    implementation 'org.koin:koin-androidx-viewmodel:2.2.2'
    implementation 'nl.joery.animatedbottombar:library:1.1.0'
    implementation 'androidx.room:room-runtime:2.4.3'
    implementation 'androidx.room:room-ktx:2.4.3'
    kapt 'androidx.room:room-compiler:2.4.3'

    implementation 'com.github.bumptech.glide:glide:4.13.0'
    kapt 'com.github.bumptech.glide:compiler:4.13.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}